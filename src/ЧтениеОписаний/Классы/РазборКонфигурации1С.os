///////////////////////////////////////////////////////////////////////////////
//
// Модуль разбора файлов конфигурации 
//
// (с) BIA Technologies, LLC	
//
///////////////////////////////////////////////////////////////////////////////

#Использовать fs

///////////////////////////////////////////////////////////////////////////////

Перем СтруктураКаталогов;
Перем ЧитательОписаний;
Перем ОписаниеКонфигурации;
Перем ДанныеКонфигурации;

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Читает описание структуры конфигурации
//	* Описание конфигурации
//	* Объекты конфигурации, включая все подсистемы
//	* Выполняет привязку объектов к подсистемам
//	* Определяет местоположение описаний (файлов и каталогов)
//
Процедура ПрочитатьСтруктуруКонфигурации() Экспорт
	

	ОписаниеКонфигурации = ПрочитатьОписаниеКонфигурации();
	
	ЗаполнитьПодсистемыОбъектовКонфигурации();
	
	ЗаполнитьИменаФайлов();

КонецПроцедуры

// Выполняет поиск всех модулей конфигурации
//
Процедура НайтиМодулиКонфигурации() Экспорт

	МодулиКонфигурации = СтруктурыОписаний.ТаблицаОписанияМодулей();

	// todo сделать чтение форм
	ФормыКонфигурации = Новый ТаблицаЗначений;
	ФормыКонфигурации.Колонки.Добавить("Родитель");
	ФормыКонфигурации.Колонки.Добавить("ЭтоУправляемаяФорма");
	ФормыКонфигурации.Колонки.Добавить("Наименование");
	
	// todo сделать чтение команд
	КомандыКонфигурации = Новый ТаблицаЗначений;
	КомандыКонфигурации.Колонки.Добавить("Родитель");
	КомандыКонфигурации.Колонки.Добавить("Наименование");
	
	ОписаниеКонфигурации.Вставить("ФормыКонфигурации", ФормыКонфигурации);
	ОписаниеКонфигурации.Вставить("КомандыКонфигурации", КомандыКонфигурации);
	ОписаниеКонфигурации.Вставить("МодулиКонфигурации", МодулиКонфигурации);
	
	Для Каждого ОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл

		ФайлыМодулей = СтруктураКаталогов.НайтиМодулиОбъекта(ОбъектКонфигурации.Наименование, ОбъектКонфигурации.Тип, Истина);
		
		Для Каждого ИмяФайлаМодуля Из ФайлыМодулей Цикл

			ПолучитьОписаниеМодуляПоИмениФайла(ИмяФайлаМодуля, ОбъектКонфигурации, ОписаниеКонфигурации);

		КонецЦикла; 

	КонецЦикла;

	МодулиКонфигурации.Сортировать("ПутьКФайлу");

КонецПроцедуры

// Возвращает объект содержащий считанное описание конфигурации
//
//  Возвращаемое значение:
//   Структура - Описание данных конфигурации
//		* СвойстваКонфигурации - Структура - Свойства конфигурации
//		* ОбъектыКонфигурации - ТаблицаЗначений - Описание объектов конфигурации, см СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//		* МодулиКонфигурации - ТаблицаЗначений - Описание модулей конфигурации, см СтруктурыОписаний.ТаблицаОписанияМодулей
//		* ФормыКонфигурации - ТаблицаЗначений - Описание форм конфигурации
//		* КомандыКонфигурации - ТаблицаЗначений - Описание команд конфигурации
//
Функция ОписаниеКонфигурации() Экспорт
	
	Возврат ОписаниеКонфигурации;
	
КонецФункции

// Читает детальное описание констант
//
//  Возвращаемое значение:
//   Массив - Коллекция описаний констант, каждый элемент это структура
//		* Имя - Имя константы
//		* Тип - Тип значения константы
//		* Описание - Описание константы
//		* Подсистема - Имя подсистемы, в которую включена константа
//		* ПодсистемаПредставление - Представление подсистемы, в которую включена константа
//
Функция ПрочитатьОписаниеКонстант() Экспорт

	МассивОписанийКонстант = Новый Массив;
	
	Для Каждого Объект Из ОписаниеКонфигурации.ПолучитьОбъектыТипа("Constant") Цикл
		
		ОписаниеИзXML = ПрочитатьФайлОписанияОбъекта(Объект.ПутьКФайлу, "Constant");

		ОписаниеКонстанты = Новый Структура("Имя, Тип, Описание, Подсистема, ПодсистемаПредставление");  
		
		ОписаниеКонстанты.Имя = ОписаниеИзXML.Наименование;
		ОписаниеКонстанты.Тип = ОписаниеИзXML.Тип;
		ОписаниеКонстанты.Описание = ОписаниеИзXML.Пояснение;

		Если ЗначениеЗаполнено(Объект.Подсистемы) Тогда
			ОписаниеКонстанты.Подсистема = Объект.Подсистемы[0].Имя;
			ОписаниеКонстанты.ПодсистемаПредставление = Объект.Подсистемы[0].ПредставлениеКратко;
		КонецЕсли;

		МассивОписанийКонстант.Добавить(ОписаниеКонстанты);

	КонецЦикла;

	Возврат МассивОписанийКонстант;

КонецФункции

// Читает и выполняет анализ содержимого модуля
//	Устанавливает реквизиты "НаборБлоков" и "Содержимое"
// Параметры:
//   СтрокаМодуль - СтрокаТаблицыЗначений - Базовое описание модуля
//
Процедура ПрочитатьСодержимоеМодуля(СтрокаМодуль) Экспорт

	Файл = Новый ТекстовыйДокумент;
	Файл.Прочитать(СтрокаМодуль.ПутьКФайлу, КодировкаТекста.UTF8NoBOM);	
	
	СодержимоеМодуля = ЧтениеМодулей.ПрочитатьМодуль(Файл, СтрокаМодуль);
	СтрокаМодуль.Содержимое = СодержимоеМодуля.Содержимое;
	СтрокаМодуль.НаборБлоков = СодержимоеМодуля.БлокиМодуля;

	Если СтрокаМодуль.ТипМодуля = ТипМодуля.ОбщийМодуль Тогда

		Текст = Новый ТекстовыйДокумент;
		Текст.Прочитать(СтрокаМодуль.Родитель.ПутьКФайлу);
		Если СтрНайти(Текст.ПолучитьТекст(), "<Global>true</Global>") Тогда

			СтрокаМодуль.ОписаниеМодуля.Вставить("Глобальный", Истина);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Удаляет из структуры объекты не включенные в подсистемы
//
Процедура УдалитьОбъектыВнеПодсистем() Экспорт
	
	Количество = ОписаниеКонфигурации.ОбъектыКонфигурации.Количество();
	
	Для Инд = 1 По Количество Цикл

		Если ОписаниеКонфигурации.ОбъектыКонфигурации[Количество - Инд].Подсистемы.Количество() = 0 Тогда
			
			ОписаниеКонфигурации.ОбъектыКонфигурации.Удалить(Количество - Инд);
			
		КонецЕсли;
		
	КонецЦикла

КонецПроцедуры

// Возвращает полное наименование объекта конфигурации или модуля
//
// Параметры:
//   СтрокаОбъект - СтрокаТаблицыЗначений - Описание объекта или модуля конфигурации
//   ДобавлятьПрефиксДляОбщихМодулей - Булево - Признак, добавлять ли тип объекта для общих модулей
//
//  Возвращаемое значение:
//   Строка - Полное имя
//
Функция ПолноеИмяОбъекта(СтрокаОбъект, ДобавлятьПрефиксДляОбщихМодулей = Истина) Экспорт

	Возврат ЧтениеОписанийБазовый.ПолноеИмяОбъекта(СтрокаОбъект, ДобавлятьПрефиксДляОбщихМодулей);
	
КонецФункции

Функция ОписаниеОбъекта(ИмяОбъекта, ТипОбъекта = Неопределено) Экспорт
	
	Если ТипОбъекта = Неопределено И СтрНайти(ИмяОбъекта, ".") Тогда
		
		Возврат ОписаниеКонфигурации.ОбъектыКонфигурации.Найти(ИмяОбъекта, "ПолноеНаименование");

	Иначе
		
		Возврат ОписаниеКонфигурации.ОбъектыКонфигурации.Найти(СтрШаблон("%1.%2", ТипОбъекта, ИмяОбъекта), "ПолноеНаименование");
		
	КонецЕсли;
	
КонецФункции

Функция ФорматВыгрузки() Экспорт
	
	Возврат СтруктураКаталогов.ФорматВыгрузки();

КонецФункции

Процедура ПрочитатьОписаниеОбъекта(ОбъектКонфигурации) Экспорт
	
	ОбъектКонфигурации.Описание = ПрочитатьФайлОписанияОбъекта(ОбъектКонфигурации.ПутьКФайлу, ОбъектКонфигурации.Тип);
	
КонецПроцедуры
//////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////

Функция ПрочитатьОписаниеКонфигурации()
	
	ФайлКонфигурации = СтруктураКаталогов.ИмяФайлаОписанияКонфигурации();
	
	Если Не ФС.ФайлСуществует(ФайлКонфигурации) Тогда

		ВызватьИсключение СтрШаблон("Файл описания конфигурации ""%1"" не обнаружен", ФайлКонфигурации);

	КонецЕсли;
	
	ОписаниеКонфигурации = ПрочитатьФайлОписанияОбъекта(ФайлКонфигурации, ТипыОбъектовКонфигурации.ИмяТипаКонфигурации());
	
	ОбъектыКонфигурации = СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации();
	
	// добавим руками объект "Configuration" для модулей приложения
	ПустаяСтрокаОбъектКонфигурации = СтруктурыОписаний.ОписаниеОбъектаКонфигурацииЗначенияПоУмолчанию();
	НовСтрока = ОбъектыКонфигурации.Добавить();
	ЗаполнитьЗначенияСвойств(НовСтрока, ПустаяСтрокаОбъектКонфигурации);
	НовСтрока.Тип = ТипыОбъектовКонфигурации.ИмяТипаКонфигурации();
	НовСтрока.Наименование = НовСтрока.Тип;
	НовСтрока.ПутьКФайлу = ФайлКонфигурации;
	
	Для Каждого Подчиненный Из ОписаниеКонфигурации.Подчиненные Цикл
		
		ЧастиИмени = СтрРазделить(Подчиненный, ".");

		НовСтрока = ОбъектыКонфигурации.Добавить();

		НовСтрока.Тип = ТипыОбъектовКонфигурации.НормализоватьИмя(ЧастиИмени[0]);
		НовСтрока.Наименование = ЧастиИмени[1];
		НовСтрока.ПолноеНаименование = Подчиненный;
		
	КонецЦикла;
	
	ОписаниеКонфигурации.Удалить("Подчиненные");

	Возврат Новый Структура("СвойстваКонфигурации, ОбъектыКонфигурации", ОписаниеКонфигурации, ОбъектыКонфигурации);

КонецФункции

Функция ПрочитатьФайлОписанияОбъекта(ПутьКФайлу, ТипОбъекта)

	ПараметрыЧтения = ПараметрыСериализации.ПараметрыСериализации(ТипОбъекта, ФорматВыгрузки());
	
	Описание = ЧитательОписаний.ПрочитатьСвойстваИзФайла(ПутьКФайлу, ПараметрыЧтения);

	Возврат Описание;
	
КонецФункции

Функция ПолучитьОбъектыТипа(Знач ТипОбъекта) Экспорт
	
	ТипОбъекта = ТипыОбъектовКонфигурации.НормализоватьИмя(ТипОбъекта);

	Возврат ОписаниеКонфигурации.ОбъектыКонфигурации.НайтиСтроки(Новый Структура("Тип", ТипОбъекта));

КонецФункции

Процедура ЗаполнитьПодсистемыОбъектовКонфигурации()
	
	// дополним объекты информацией о подсистемах
	ПодсистемыКонфигурации = ПрочитатьПодсистемыКонфигурации();

	ОписаниеКонфигурации.Вставить("ПодсистемыКонфигурации", ПодсистемыКонфигурации);

	Для Каждого ОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл
		
		Подсистемы = ПодсистемыКонфигурации.НайтиСтроки(Новый Структура("ОбъектМетаданных", ОбъектКонфигурации.Тип + "." + ОбъектКонфигурации.Наименование));
		
		Если Подсистемы.Количество()  Тогда

			// ставим первую
			ОбъектКонфигурации.Подсистемы = Подсистемы;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьОписаниеМодуляПоИмениФайла(Знач ИмяФайлаМодуля, ОбъектКонфигурации, ОписаниеКонфигурации)

	НовыйМодульКонфигурации = Неопределено;
	
	ПустаяСтрокаМодульКонфигурации = Новый Структура(
		"ПутьКФайлу,	ТипМодуля,	ОписаниеМодуля,		Родитель, 		РодительФорма, 	РодительКоманда, 	НаборБлоков, 	Содержимое", 
		"", 			"", 		Новый Структура,	Неопределено, 	Неопределено, 	Неопределено, 		Неопределено, 	Неопределено);
	
	ТипЭтогоМодуля = ТипМодуля.ТипМодуляПоИмениФайла(ИмяФайлаМодуля);

	Если Не ПустаяСтрока(ТипЭтогоМодуля) Тогда
		
		ФормаОбъекта = Неопределено;
		КомандаОбъекта = Неопределено;
		
		Если ТипЭтогоМодуля = ТипМодуля.МодульОбъекта
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульМенеджера
			ИЛИ ТипЭтогоМодуля = ТипМодуля.ОбщийМодуль
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульУправляемогоПриложения
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульСеанса
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульВнешнегоСоединения
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульОбычногоПриложения  Тогда

		ИначеЕсли ТипМодуля.ЭтоМодульФормы(ТипЭтогоМодуля) Тогда

			ФормаОбъекта = ОписаниеКонфигурации.ФормыКонфигурации.Добавить();
			ФормаОбъекта.Родитель = ОбъектКонфигурации;
			ФормаОбъекта.ЭтоУправляемаяФорма = ТипЭтогоМодуля = ТипМодуля.МодульУправляемойФормы;
			ФормаОбъекта.Наименование = ПолучитьИмяФормыИзИмениФайлаМодуля(ИмяФайлаМодуля);

		ИначеЕсли ТипЭтогоМодуля = ТипМодуля.МодульКоманды Тогда

			КомандаОбъекта = ОписаниеКонфигурации.КомандыКонфигурации.Добавить();
			КомандаОбъекта.Родитель = ОбъектКонфигурации;
			КомандаОбъекта.Наименование = ПолучитьИмяКомандыИзИмениФайлаМодуля(ИмяФайлаМодуля);

		Иначе

			ВызватьИсключение "Тип модуля: " + ТипЭтогоМодуля + " не имеет алгоритма разбора";

		КонецЕсли;
		
		НовыйМодульКонфигурации = ОписаниеКонфигурации.МодулиКонфигурации.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйМодульКонфигурации, ПустаяСтрокаМодульКонфигурации);
		НовыйМодульКонфигурации.ТипМодуля = ТипЭтогоМодуля;
		НовыйМодульКонфигурации.ПутьКФайлу = ИмяФайлаМодуля;
		НовыйМодульКонфигурации.Родитель = ОбъектКонфигурации;
		НовыйМодульКонфигурации.РодительФорма = ФормаОбъекта;
		НовыйМодульКонфигурации.РодительКоманда = КомандаОбъекта;
		
	КонецЕсли;

	Если НовыйМодульКонфигурации <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ПустаяСтрокаМодульКонфигурации, НовыйМодульКонфигурации);
		
	КонецЕсли;
	
	Возврат ПустаяСтрокаМодульКонфигурации;

КонецФункции

///////////////////////////////////////////////////////////////////

Процедура ЗаполнитьИменаФайлов()

	Для Каждого СтрокаОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл
		
		Если ПустаяСтрока(СтрокаОбъектКонфигурации.ПутьКФайлу) Тогда
			
			СтрокаОбъектКонфигурации.ПутьКФайлу = СтруктураКаталогов.ИмяФайлаОписанияОбъекта(СтрокаОбъектКонфигурации.Наименование, СтрокаОбъектКонфигурации.Тип);
			
		КонецЕсли;

	КонецЦикла

КонецПроцедуры

///////////////////////////////////////////////////////////////////

Функция ПолучитьИмяФормыИзИмениФайлаМодуля(ПолноеИмяФайла)

	МассивЧастейИмени = СтрРазделить(ПолноеИмяФайла, "\");
	Если МассивЧастейИмени.Количество() > 3 Тогда

		Номер = 2;
		Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];
        Если Родитель = "Form" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Если Родитель = "Ext" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Возврат Родитель;

	Иначе

		ВызватьИсключение "Ошибочная структура имени файла: " + ПолноеИмяФайла;

	КонецЕсли;

	Возврат "";

КонецФункции

Функция ПолучитьИмяКомандыИзИмениФайлаМодуля(ПолноеИмяФайла)

	МассивЧастейИмени = СтрРазделить(ПолноеИмяФайла, "\");
	Если МассивЧастейИмени.Количество() > 3 Тогда

		Номер = 2;
		Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];
        
		Если Родитель = "Ext" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Возврат Родитель;

	Иначе

		ВызватьИсключение "Ошибочная структура имени файла: " + ПолноеИмяФайла;

	КонецЕсли;

	Возврат "";

КонецФункции

///////////////////////////////////////////////////////////////////

Функция ПрочитатьПодсистемыКонфигурации()

	ОписаниеПодсистем = СтруктурыОписаний.ТаблицаОписанияПодсистем();

	Подсистемы = ПолучитьОбъектыТипа("Подсистема");
	
	Для Каждого Подсистема Из Подсистемы Цикл
		
		ПрочитатьПодсистему(ОписаниеПодсистем, Подсистема, Истина, Неопределено, Неопределено);

	КонецЦикла;
	
	Возврат ОписаниеПодсистем;

КонецФункции

Процедура ПрочитатьПодсистему(ОписаниеПодсистем, ОбъектКонфигурации, Знач Визуальная, Знач Родитель, РодительскаяПодсистема)
	
	ТипПодсистема = ТипыОбъектовКонфигурации.ИмяТипаПодсистема();
	ИмяФайлаОписание = СтруктураКаталогов.ИмяФайлаОписанияОбъекта(ОбъектКонфигурации.Наименование, ОбъектКонфигурации.Тип);
	

	СвойстваОписания = ПрочитатьФайлОписанияОбъекта(ИмяФайлаОписание, ТипПодсистема);
	
	ОбъектКонфигурации.Описание = СвойстваОписания;
	ОбъектКонфигурации.ПутьКФайлу = ИмяФайлаОписание;
	ОбъектКонфигурации.ПутьККаталогу = СтруктураКаталогов.КаталогФайловОбъекта(ОбъектКонфигурации.Наименование, ОбъектКонфигурации.Тип);
	
	Визуальная = Визуальная И СвойстваОписания.ВключатьВКомандныйИнтерфейс;

	ПредставлениеПодсистемы = ?(РодительскаяПодсистема = Неопределено, СвойстваОписания.Синоним, РодительскаяПодсистема.Представление + "/" + СвойстваОписания.Синоним);
	
	Если СвойстваОписания.Состав.Количество() Тогда
		Состав = СвойстваОписания.Состав;
	Иначе
		Состав = Новый Массив();
		Состав.Добавить(Неопределено);
	КонецЕсли;
	
	Для Каждого ОбъектМетаданных Из Состав Цикл
		
		ЭтаПодсистема = ОписаниеПодсистем.Добавить();
		ЭтаПодсистема.Имя = ОбъектКонфигурации.Наименование;
		ЭтаПодсистема.ИмяКратко = СвойстваОписания.Наименование;
		ЭтаПодсистема.Представление = ПредставлениеПодсистемы;
		ЭтаПодсистема.ПредставлениеКратко = СвойстваОписания.Синоним;
		ЭтаПодсистема.ПодсистемаОписание = СвойстваОписания.Комментарий;
		ЭтаПодсистема.ОбъектМетаданных = ОбъектМетаданных;
		ЭтаПодсистема.Визуальная = Визуальная;
		ЭтаПодсистема.Родитель = РодительскаяПодсистема;
		
	КонецЦикла;

	Для Каждого ПолноеИмяПодсистемы Из СвойстваОписания.Подчиненные Цикл
		
		ВложеннаяПодсистема = ОписаниеКонфигурации.ОбъектыКонфигурации.Добавить();
		ВложеннаяПодсистема.Тип = ТипПодсистема;
		ВложеннаяПодсистема.Наименование = ОбъектКонфигурации.Наименование + "." + СтрРазделить(ПолноеИмяПодсистемы, ".")[1];
		ВложеннаяПодсистема.Родитель = ОбъектКонфигурации;

		ПрочитатьПодсистему(ОписаниеПодсистем, ВложеннаяПодсистема, Визуальная, ОбъектКонфигурации, ЭтаПодсистема)
		
	КонецЦикла;

КонецПроцедуры

Процедура ПриСозданииОбъекта(КаталогИсходников)
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, "Авто");
	
	Если СтруктураКаталогов.ФорматВыгрузки() = "EDT" Тогда
		
		ЧитательОписаний = ЧтениеОписанийEDT;
		
	Иначе
		
		ЧитательОписаний = ЧтениеОписанийКонфигуратор;
		
	КонецЕсли;

КонецПроцедуры