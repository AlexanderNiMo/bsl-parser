///////////////////////////////////////////////////////////////////////////////
//
// Модуль разбора файлов конфигурации 
//
// (с) BIA Technologies, LLC	
//
///////////////////////////////////////////////////////////////////////////////

#Использовать fs
#Использовать "../ЧтениеМодулей"

///////////////////////////////////////////////////////////////////////////////

Перем СтруктураКаталогов;
Перем ЧитательОписаний;
Перем ОписаниеКонфигурации;
Перем ДанныеКонфигурации;

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

Процедура ПрочитатьСтруктуруКонфигурации() Экспорт
	
	ОписаниеКонфигурации = ПрочитатьОписаниеКонфигурации();
	
	ЗаполнитьПодсистемыОбъектовКонфигурации();
	
	ЗаполнитьИменаФайлов();

КонецПроцедуры

Функция НайтиМодулиКонфигурации()Экспорт

	МодулиКонфигурации = СтруктурыОписаний.ТаблицаОписанияМодулей();

	// todo сделать чтение форм
	ФормыКонфигурации = Новый ТаблицаЗначений;
	ФормыКонфигурации.Колонки.Добавить("Родитель");
	ФормыКонфигурации.Колонки.Добавить("ЭтоУправляемаяФорма");
	ФормыКонфигурации.Колонки.Добавить("Наименование");
	
	// todo сделать чтение команд
	КомандыКонфигурации = Новый ТаблицаЗначений;
	КомандыКонфигурации.Колонки.Добавить("Родитель");
	КомандыКонфигурации.Колонки.Добавить("Наименование");
	
	ОписаниеКонфигурации.Вставить("ФормыКонфигурации", ФормыКонфигурации);
	ОписаниеКонфигурации.Вставить("КомандыКонфигурации", КомандыКонфигурации);
	ОписаниеКонфигурации.Вставить("МодулиКонфигурации", МодулиКонфигурации);
	
	Для Каждого ОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл

		ФайлыМодулей = СтруктураКаталогов.НайтиМодулиОбъекта(ОбъектКонфигурации.Наименование, ОбъектКонфигурации.Тип, Истина);
		
		Для Каждого ИмяФайлаМодуля Из ФайлыМодулей Цикл

			ПолучитьОписаниеМодуляПоИмениФайла(ИмяФайлаМодуля, ОбъектКонфигурации, ОписаниеКонфигурации);

		КонецЦикла; 

	КонецЦикла;

	МодулиКонфигурации.Сортировать("ПутьКФайлу");
	Возврат ОписаниеКонфигурации;

КонецФункции

Функция ОписаниеКонфигурации() Экспорт
	
	Возврат ОписаниеКонфигурации;
	
КонецФункции

Функция ПрочитатьОписаниеКонстант()Экспорт

	Фильтр = Новый Структура("Тип", "Constant");
	
	СтрокиКонстант = ОписаниеКонфигурации.ОбъектыКонфигурации.НайтиСтроки(Фильтр);
	
	МассивОписанийКонстант = Новый Массив;
	
	Для Каждого Объект Из СтрокиКонстант Цикл
		
		ОписаниеИзXML = ПолучитьОписаниеКонстанты(Объект.ПутьКФайлуСОписанием);
		
		ОписаниеКонстанты = Новый Структура("Имя, Тип, Описание, Подсистема, ПодсистемаПредставление");  
		
		ОписаниеКонстанты.Имя = Объект.Наименование;
		ОписаниеКонстанты.Тип = ЧтениеОписанийБазовый.ПреобразоватьТип(ОписаниеИзXML.ТипКонстанты);
		ОписаниеКонстанты.Описание = ОписаниеИзXML.ТекстОписания;
		
		Если ЗначениеЗаполнено(Объект.Подсистемы) Тогда
			ОписаниеКонстанты.Подсистема = Объект.Подсистемы[0].Имя;
			ОписаниеКонстанты.ПодсистемаПредставление = Объект.Подсистемы[0].ПредставлениеКратко;
		КонецЕсли;

		МассивОписанийКонстант.Добавить(ОписаниеКонстанты);

	КонецЦикла;

	Возврат МассивОписанийКонстант;

КонецФункции

Процедура ПрочитатьСодержимоеМодуля(СтрокаМодуль)Экспорт

	Файл = Новый ТекстовыйДокумент;
	Файл.Прочитать(СтрокаМодуль.ПутьКФайлу, КодировкаТекста.UTF8NoBOM);	
	
	СодержимоеМодуля = ЧтениеМодулей.ПрочитатьМодуль(Файл);
	СтрокаМодуль.Содержимое = СодержимоеМодуля.Содержимое;

	ЧтениеМодулей.ДополнитьБлокиМодуля(СодержимоеМодуля.БлокиМодуля, Файл, СтрокаМодуль);
	СтрокаМодуль.НаборБлоков = СодержимоеМодуля.БлокиМодуля;

	Если СтрокаМодуль.ТипМодуля = ТипМодуля.ОбщийМодуль Тогда

		Текст = Новый ТекстовыйДокумент;
		Текст.Прочитать(СтрокаМодуль.Родитель.ПутьКФайлуСОписанием);
		Если СтрНайти(Текст.ПолучитьТекст(), "<Global>true</Global>") Тогда

			СтрокаМодуль.ОписаниеМодуля.Вставить("Глобальный", Истина);

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура УдалитьОбъектыВнеПодсистем() Экспорт
	
	Количество = ОписаниеКонфигурации.ОбъектыКонфигурации.Количество();
	
	Для Инд = 1 По Количество Цикл

		Если ОписаниеКонфигурации.ОбъектыКонфигурации[Количество - Инд].Подсистемы.Количество() = 0 Тогда
			
			ОписаниеКонфигурации.ОбъектыКонфигурации.Удалить(Количество - Инд);
			
		КонецЕсли;
		
	КонецЦикла

КонецПроцедуры

Функция ПолноеИмяМодуля(СтрокаМодуль) Экспорт

	Возврат ЧтениеОписанийБазовый.ПолноеИмяМодуля(СтрокаМодуль); 

КонецФункции

Функция ПолноеИмяОбъекта(СтрокаМодуль, ДобавлятьПрефиксДляОбщихМодулей = ИСТИНА) Экспорт

	Возврат ЧтениеОписанийБазовый.ПолноеИмяОбъекта(СтрокаМодуль, ДобавлятьПрефиксДляОбщихМодулей);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// УСТАРЕВШИЙ ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

Функция ПрочитатьДеревоКонфигурации() Экспорт
	
	ПрочитатьСтруктуруКонфигурации();
	
	Возврат ОписаниеКонфигурации;
	
КонецФункции

Функция СформироватьОписаниеОбъекта(КореньОписания)

	ОписаниеОбъекта = Новый Структура;
	ВеткаСвойства = КореньОписания.Найти("Properties", "Имя");
	Если ВеткаСвойства <> Неопределено Тогда

	КонецЕсли;

	ВеткаПодчиненныеОбъекты = КореньОписания.Найти("ChildObjects", "Имя");
	Если ВеткаПодчиненныеОбъекты <> Неопределено И ТипЗнч(ВеткаПодчиненныеОбъекты.Значение) = Тип("ТаблицаЗначений") Тогда

		Формы = СформироватьОписаниеОбъектаФормы(ВеткаПодчиненныеОбъекты.Значение);
		Если Формы <> Неопределено Тогда

			ОписаниеОбъекта.Вставить("Формы", Формы);

		КонецЕсли;

		Реквизиты = СформироватьОписаниеОбъектаРеквизиты(ВеткаПодчиненныеОбъекты.Значение);
		Если Реквизиты <> Неопределено Тогда

			ОписаниеОбъекта.Вставить("Реквизиты", Реквизиты);

		КонецЕсли;

		ТЧ = СформироватьОписаниеОбъектаТабличныеЧасти(ВеткаПодчиненныеОбъекты.Значение);
		Если ТЧ <> Неопределено Тогда

			ОписаниеОбъекта.Вставить("ТабличныеЧасти", ТЧ);

		КонецЕсли;

	КонецЕсли;

	Возврат ОписаниеОбъекта;

КонецФункции

Функция СформироватьОписаниеОбъектаФормы(ПодчиненныеОбъекты)

	СтрФормы = ПодчиненныеОбъекты.НайтиСтроки(Новый Структура("Имя", "Form"));
	Если СтрФормы.Количество() Тогда

		Формы = Новый Массив;
		Для Каждого СтрФорма Из СтрФормы Цикл

			Формы.ДобавитЬ(СтрФорма.Значение);

		КонецЦикла;

		Возврат Формы;

	КонецЕсли;

	Возврат Неопределено;

КонецФункции

Функция СформироватьОписаниеОбъектаРеквизиты(ПодчиненныеОбъекты)

	СтрАтрибуты = ПодчиненныеОбъекты.НайтиСтроки(Новый Структура("Имя", "Attribute"));
	Если СтрАтрибуты.Количество() Тогда

		Реквизиты = Новый ТаблицаЗначений;
		Реквизиты.Колонки.Добавить("Имя");
		Для Каждого СтрАтрибут Из СтрАтрибуты Цикл
			
			ОписаниеАтрибута = СтрАтрибут.Значение.Найти("Properties", "Имя");
			Если ОписаниеАтрибута = Неопределено Тогда

				Продолжить;

			КонецЕсли;

			СтрНаименования = ОписаниеАтрибута.Значение.Найти("Name", "Имя");
			Если СтрНаименования = Неопределено Тогда

				Продолжить;

			КонецЕсли;
			
			НовыйРеквизит = Реквизиты.Добавить();
			НовыйРеквизит.Имя = СтрНаименования.Значение;
			Для Каждого ЭлОписания Из ОписаниеАтрибута.Значение Цикл

				Если ЭлОписания.Имя = "Name" Тогда

					Продолжить;

				КонецЕсли;

				Если Реквизиты.Колонки.Найти(ЭлОписания.Имя) = Неопределено Тогда

					Попытка

						Реквизиты.Колонки.Добавить(ЭлОписания.Имя);

					Исключение

						Продолжить;
						
					КонецПопытки;

				КонецЕсли;

				НовыйРеквизит[ЭлОписания.Имя] = ЭлОписания.Значение;

			КонецЦикла

		КонецЦикла;

		Возврат Реквизиты;

	КонецЕсли;

	Возврат Неопределено;

КонецФункции

Функция СформироватьОписаниеОбъектаТабличныеЧасти(ПодчиненныеОбъекты)

	СтрТЧ = ПодчиненныеОбъекты.НайтиСтроки(Новый Структура("Имя", "TabularSection"));
	Если СтрТЧ.Количество() Тогда

		ТЧ = Новый ТаблицаЗначений;
		ТЧ.Колонки.Добавить("Имя");
		ТЧ.Колонки.Добавить("Описание");
		ТЧ.Колонки.Добавить("Реквизиты");
		Для Каждого Стр Из СтрТЧ Цикл
			
			СтрОписание = СформироватьОписаниеОбъекта(Стр.Значение);
			Если СтрОписание.Свойство("Описание") Тогда
				
				СтрНаименования = СтрОписание.Описание.Найти("Name", "Имя");
				Если СтрНаименования <> Неопределено Тогда
				
					НоваяТЧ = ТЧ.Добавить();
					НоваяТЧ.Имя = СтрНаименования.Значение;
					НоваяТЧ.Описание = СтрОписание.Описание;

					Если СтрОписание.Свойство("Реквизиты") Тогда

						НоваяТЧ.Реквизиты = СтрОписание.Реквизиты;

					КонецЕсли;

				КонецЕсли; 
				
			КонецЕсли; 

		КонецЦикла;

		Возврат ТЧ;

	КонецЕсли;

	Возврат Неопределено;

КонецФункции

//////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////

Функция ПрочитатьОписаниеКонфигурации()
	
	ФайлКонфигурации = СтруктураКаталогов.ИмяФайлаОписанияКонфигурации();
	
	Если Не ФС.ФайлСуществует(ФайлКонфигурации) Тогда

		ВызватьИсключение СтрШаблон("Файл описания конфигурации ""%1"" не обнаружен", ФайлКонфигурации);

	КонецЕсли;

	ОписаниеКонфигурации = ЧитательОписаний.ПрочитатьОписаниеКонфигурации(ФайлКонфигурации);
	
	СвойстваКонфигурации = ОписаниеКонфигурации.СвойстваКонфигурации;
	ОбъектыКонфигурации = ОписаниеКонфигурации.ОбъектыКонфигурации;

	// добавим руками объект "Configuration" для модулей приложения
	ПустаяСтрокаОбъектКонфигурации = СтруктурыОписаний.ОписаниеОбъектаКонфигурацииЗначенияПоУмолчанию();
	НовСтрока = ОбъектыКонфигурации.Добавить();
	ЗаполнитьЗначенияСвойств(НовСтрока, ПустаяСтрокаОбъектКонфигурации);
	НовСтрока.Тип = "Configuration";
	НовСтрока.Наименование = "Configuration";
	НовСтрока.ПутьКФайлуСОписанием = ФайлКонфигурации;
	
	Возврат Новый Структура("СвойстваКонфигурации, ОбъектыКонфигурации", СвойстваКонфигурации, ОбъектыКонфигурации);

КонецФункции

Процедура ЗаполнитьПодсистемыОбъектовКонфигурации()
	
	// дополним объекты информацией о подсистемах
	ПодсистемыКонфигурации = ПрочитатьПодсистемыКонфигурации();

	ОписаниеКонфигурации.Вставить("ПодсистемыКонфигурации", ПодсистемыКонфигурации);

	Для Каждого ОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл
		
		Подсистемы = ПодсистемыКонфигурации.НайтиСтроки(Новый Структура("ОбъектМетаданных", ОбъектКонфигурации.Тип + "." + ОбъектКонфигурации.Наименование));
		
		Если Подсистемы.Количество()  Тогда

			// ставим первую
			ОбъектКонфигурации.Подсистемы = Подсистемы;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

Функция ПолучитьОписаниеМодуляПоИмениФайла(Знач ИмяФайлаМодуля, ОбъектКонфигурации, ОписаниеКонфигурации)Экспорт

	НовыйМодульКонфигурации = Неопределено;
	
	ПустаяСтрокаМодульКонфигурации = Новый Структура(
		"ПутьКФайлу,	ТипМодуля,	ОписаниеМодуля,		Родитель, 		РодительФорма, 	РодительКоманда, 	НаборБлоков, 	Содержимое", 
		"", 			"", 		Новый Структура,	Неопределено, 	Неопределено, 	Неопределено, 		Неопределено, 	Неопределено);
	
	ТипЭтогоМодуля = ТипМодуля.ТипМодуляПоИмениФайла(ИмяФайлаМодуля);

	Если Не ПустаяСтрока(ТипЭтогоМодуля) Тогда
		
		ФормаОбъекта = Неопределено;
		КомандаОбъекта = Неопределено;
		
		Если ТипЭтогоМодуля = ТипМодуля.МодульОбъекта
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульМенеджера
			ИЛИ ТипЭтогоМодуля = ТипМодуля.ОбщийМодуль
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульУправляемогоПриложения
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульСеанса
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульВнешнегоСоединения
			ИЛИ ТипЭтогоМодуля = ТипМодуля.МодульОбычногоПриложения  Тогда

		ИначеЕсли ТипМодуля.ЭтоМодульФормы(ТипЭтогоМодуля) Тогда

			ФормаОбъекта = ОписаниеКонфигурации.ФормыКонфигурации.Добавить();
			ФормаОбъекта.Родитель = ОбъектКонфигурации;
			ФормаОбъекта.ЭтоУправляемаяФорма = ТипЭтогоМодуля = ТипМодуля.МодульУправляемойФормы;
			ФормаОбъекта.Наименование = ПолучитьИмяФормыИзИмениФайлаМодуля(ИмяФайлаМодуля);

		ИначеЕсли ТипЭтогоМодуля = ТипМодуля.МодульКоманды Тогда

			КомандаОбъекта = ОписаниеКонфигурации.КомандыКонфигурации.Добавить();
			КомандаОбъекта.Родитель = ОбъектКонфигурации;
			КомандаОбъекта.Наименование = ПолучитьИмяКомандыИзИмениФайлаМодуля(ИмяФайлаМодуля);

		Иначе

			ВызватьИсключение "Тип модуля: " + ТипЭтогоМодуля + " не имеет алгоритма разбора";

		КонецЕсли;
		
		НовыйМодульКонфигурации = ОписаниеКонфигурации.МодулиКонфигурации.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйМодульКонфигурации, ПустаяСтрокаМодульКонфигурации);
		НовыйМодульКонфигурации.ТипМодуля = ТипЭтогоМодуля;
		НовыйМодульКонфигурации.ПутьКФайлу = ИмяФайлаМодуля;
		НовыйМодульКонфигурации.Родитель = ОбъектКонфигурации;
		НовыйМодульКонфигурации.РодительФорма = ФормаОбъекта;
		НовыйМодульКонфигурации.РодительКоманда = КомандаОбъекта;
		
	КонецЕсли;

	Если НовыйМодульКонфигурации <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ПустаяСтрокаМодульКонфигурации, НовыйМодульКонфигурации);
		
	КонецЕсли;
	
	Возврат ПустаяСтрокаМодульКонфигурации;

КонецФункции

///////////////////////////////////////////////////////////////////

Функция ПолучитьОписаниеКонстанты(ПутьКФайлу)
	
	ПараметрыОписанияКонстанты = СтруктурыОписаний.ОписаниеКонстанты();
	
	Описание = ЧитательОписаний.ПрочитатьСвойстваИзФайла(ПутьКФайлу, ПараметрыОписанияКонстанты);

	РезультатПоиска = Новый Структура("ТипКонстанты, ТекстОписания", Описание.Тип , Описание.Пояснение);

	Возврат РезультатПоиска;
	
КонецФункции

///////////////////////////////////////////////////////////////////

Процедура ЗаполнитьИменаФайлов()

	Для Каждого СтрокаОбъектКонфигурации Из ОписаниеКонфигурации.ОбъектыКонфигурации Цикл
		
		СтрокаОбъектКонфигурации.ПутьКФайлуСОписанием = СтруктураКаталогов.ИмяФайлаОписанияОбъекта(СтрокаОбъектКонфигурации.Наименование, СтрокаОбъектКонфигурации.Тип);

	КонецЦикла

КонецПроцедуры

///////////////////////////////////////////////////////////////////

Функция ПолучитьИмяФормыИзИмениФайлаМодуля(ПолноеИмяФайла)

	МассивЧастейИмени = СтрРазделить(ПолноеИмяФайла, "\");
	Если МассивЧастейИмени.Количество() > 3 Тогда

		Номер = 2;
		Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];
        Если Родитель = "Form" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Если Родитель = "Ext" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Возврат Родитель;

	Иначе

		ВызватьИсключение "Ошибочная структура имени файла: " + ПолноеИмяФайла;

	КонецЕсли;

	Возврат "";

КонецФункции

Функция ПолучитьИмяКомандыИзИмениФайлаМодуля(ПолноеИмяФайла)

	МассивЧастейИмени = СтрРазделить(ПолноеИмяФайла, "\");
	Если МассивЧастейИмени.Количество() > 3 Тогда

		Номер = 2;
		Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];
        
		Если Родитель = "Ext" Тогда

			Номер = Номер + 1;
			Родитель = МассивЧастейИмени[МассивЧастейИмени.Количество() - Номер];

		КонецЕсли;

		Возврат Родитель;

	Иначе

		ВызватьИсключение "Ошибочная структура имени файла: " + ПолноеИмяФайла;

	КонецЕсли;

	Возврат "";

КонецФункции

///////////////////////////////////////////////////////////////////

Функция ПрочитатьПодсистемыКонфигурации()

	ОписаниеПодсистем = СтруктурыОписаний.ТаблицаОписанияПодсистем();

	Для Каждого ФайлыПодсистемы Из СтруктураКаталогов.НайтиФайлыПодсистем() Цикл

		ПрочитатьПодсистему(ФайлыПодсистемы, ОписаниеПодсистем, "", "", Неопределено, Неопределено);
		
	КонецЦикла;
	
	Возврат ОписаниеПодсистем;

КонецФункции

Процедура ПрочитатьПодсистему(ФайлыПодсистемы, ОписаниеПодсистем, Знач ПодсистемаИмя, Знач ПодсистемаПредставление, Знач Визуальная, Знач Родитель)

	СвойстваОписания = ЧитательОписаний.ПрочитатьСвойстваИзФайла(ФайлыПодсистемы.ФайлОписания, СтруктурыОписаний.ОписаниеПодсистемы());
	
	ВКомандномИнтерфейсе = СвойстваОписания.ВключатьВКомандныйИнтерфейс = "true";
	
	Визуальная = ?(Визуальная = Неопределено, ВКомандномИнтерфейсе, Мин(Визуальная, ВКомандномИнтерфейсе));
	ПодсистемаИмя = ПодсистемаИмя + ?(ПустаяСтрока(ПодсистемаИмя), "", ".") + СвойстваОписания.Наименование;
	ПодсистемаПредставление = ПодсистемаПредставление + ?(ПустаяСтрока(ПодсистемаПредставление), "", "/") + СвойстваОписания.Синоним;
	
	Если Родитель <> Неопределено Тогда // В списке объектов конфигурации подсистемы только первого уровня
		
		СтрОбъект = ОписаниеКонфигурации.ОбъектыКонфигурации.Добавить();
		ЗаполнитьЗначенияСвойств(СтрОбъект, СтруктурыОписаний.ОписаниеОбъектаКонфигурацииЗначенияПоУмолчанию());
		СтрОбъект.Тип = "Subsystem";
		СтрОбъект.Наименование = ПодсистемаИмя;
		СтрОбъект.ПутьКФайлуСОписанием = ФайлыПодсистемы.ФайлОписания;
		СтрОбъект.ПутьККаталогу = ФайлыПодсистемы.КаталогОписания;
		СтрОбъект.Описание = СвойстваОписания;
		СтрОбъект.Родитель = ОписаниеКонфигурации.ОбъектыКонфигурации.Найти(Родитель.Имя, "Наименование");

	КонецЕсли;
	
	Если СвойстваОписания.Состав.Количество() Тогда
		
		Для Каждого ОбъектМетаданных Из СвойстваОписания.Состав Цикл
			
			ЭтаПодсистема = ОписаниеПодсистем.Добавить();
			ЭтаПодсистема.Имя = ПодсистемаИмя;
			ЭтаПодсистема.ИмяКратко = СвойстваОписания.Наименование;
			ЭтаПодсистема.Представление = ПодсистемаПредставление;
			ЭтаПодсистема.ПредставлениеКратко = СвойстваОписания.Синоним;
			ЭтаПодсистема.ПодсистемаОписание = СвойстваОписания.Комментарий;
			ЭтаПодсистема.ОбъектМетаданных = ОбъектМетаданных;
			ЭтаПодсистема.Визуальная = Визуальная;
			ЭтаПодсистема.Родитель = Родитель;
			
		КонецЦикла;
		
	Иначе
		
		ЭтаПодсистема = ОписаниеПодсистем.Добавить();
		ЭтаПодсистема.Имя = ПодсистемаИмя;
		ЭтаПодсистема.ИмяКратко = СвойстваОписания.Наименование;
		ЭтаПодсистема.Представление = ПодсистемаПредставление;
		ЭтаПодсистема.ПредставлениеКратко = СвойстваОписания.Синоним;
		ЭтаПодсистема.ПодсистемаОписание = СвойстваОписания.Комментарий;
		ЭтаПодсистема.Визуальная = Визуальная;
		ЭтаПодсистема.Родитель = Родитель;		

	КонецЕсли;
	
	Для Каждого ФайлыВложеннойПодсистемы Из ФайлыПодсистемы.Вложенные Цикл

		ПрочитатьПодсистему(ФайлыВложеннойПодсистемы, ОписаниеПодсистем, ПодсистемаИмя, ПодсистемаПредставление, Визуальная, ЭтаПодсистема)
		
	КонецЦикла;

КонецПроцедуры

Процедура ПриСозданииОбъекта(КаталогИсходников)
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, "Авто");
	
	Если СтруктураКаталогов.ФорматВыгрузки() = "EDT" Тогда
		
		ЧитательОписаний = ЧтениеОписанийEDT;
		
	Иначе
		
		ЧитательОписаний = ЧтениеОписанийКонфигуратор;
		
	КонецЕсли;

КонецПроцедуры