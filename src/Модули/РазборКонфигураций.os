///////////////////////////////////////////////////////////////////////////////
//
// Корневой объект библиотеки. Содержит методы конструкторы.
//	Создает объекты для работы с выгрузками описаний конфигураций и расширений
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Создает объект для чтение и редактирования описания конфигурации
//	Выполняет чтение структуры описания
//
// Параметры:
//   КаталогИсходников - Строка - Каталог, который содержит описание конфигурации
//   ФорматВыгрузки - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//								Если указать "Авто", произойдет автоматическое определение формата выгрузки
//
//  Возвращаемое значение:
//   Конфигурация - Объект для формирования описания
//
Функция ЗагрузитьКонфигурацию(КаталогИсходников, ФорматВыгрузки = "Авто") Экспорт
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, ФорматВыгрузки, Истина);
	
	Конфигурация = Новый Конфигурация(СтруктураКаталогов);
	Конфигурация.ПрочитатьСтруктуру();

	Возврат Конфигурация;

КонецФункции

// Создает объект для чтение и редактирования описания расширения
//	Выполняет чтение структуры описания
//
// Параметры:
//   КаталогИсходников - Строка - Каталог, который содержит описание расширения
//   ФорматВыгрузки - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//								Если указать "Авто", произойдет автоматическое определение формата выгрузки
//
//  Возвращаемое значение:
//   Расширение - Объект для формирования описания
//
Функция ЗагрузитьРасширение(КаталогИсходников, ФорматВыгрузки = "Авто") Экспорт
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, ФорматВыгрузки, Истина);
	
	Расширение = Новый Расширение(СтруктураКаталогов);
	Расширение.ПрочитатьСтруктуру();
	
	Возврат Расширение;
	
КонецФункции

// Создает объект для формирования описания конфигурации
//
// Параметры:
//   КаталогИсходников - Строка - Каталог, в который записываются описания
//   ФорматВыгрузки - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//
//  Возвращаемое значение:
//   Конфигурация - Объект для формирования описания
//
Функция СоздатьНовуюКонфигурацию(КаталогИсходников, ФорматВыгрузки) Экспорт
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, ФорматВыгрузки, Истина);
	
	Конфигурация = Новый Конфигурация(СтруктураКаталогов);

	РедакторКонфигурации.ИнициализироватьСозданиеНового(Конфигурация);

	Возврат Конфигурация;

КонецФункции

// Создает объект для формирования описания расширения
//
// Параметры:
//   КаталогИсходников - Строка - Каталог, в который записываются описания
//   ФорматВыгрузки - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//
//  Возвращаемое значение:
//   Расширение - Объект для формирования описания
//
Функция СоздатьНовоеРасширение(КаталогИсходников, ФорматВыгрузки) Экспорт
	
	СтруктураКаталогов = Новый СтруктураКаталоговКонфигурации(КаталогИсходников, ФорматВыгрузки, Истина);
	
	Расширение = Новый Расширение(СтруктураКаталогов);

	РедакторРасширения.ИнициализироватьСозданиеНового(Расширение);

	Возврат Расширение;

КонецФункции

// Создает пустое описание объекта конфигурации
//	Возвращает структуру, ключи которой - имена свойств описания как в конфигураторе
//	Используется для регистрации новых объектов в конфигурации
//
// Параметры:
//   ТипОбъекта - Строка - Тип объекта конфигурации, см ТипыОбъектовКонфигурации, ОбъектыКонфигурации.md
//   Наименование - Строка - Наименование объекта
//
//  Возвращаемое значение:
//   Структура - пустышка описания объекта
//
Функция СоздатьОбъектКонфигурации(ТипОбъекта, Наименование) Экспорт
	
	Возврат СтруктурыОписаний.СоздатьОбъект(ТипОбъекта, Наименование);
	
КонецФункции