///////////////////////////////////////////////////////////////////////////////
//
// Информация о типах конфигурации
//
///////////////////////////////////////////////////////////////////////////////

Перем ОбъектыКонфигурации;
Перем ХэшПоиска;

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Возвращает информацию о типе объекта конфигурации по имени типа
//
// Параметры:
//   ИмяТипаОбъектаКонфигурации - Строка - Имя типа объекта, например, Справочник, Document, Перечисления
//
//  Возвращаемое значение:
//   Структура - Описание типа
//		* Наименование - Русское наименование типа в единственном числе
//		* НаименованиеКоллекции - Русское наименование типа во множественном числе
//		* НаименованиеEng - Английское наименование типа в единственном числе
//		* НаименованиеКоллекцииEng - Английское наименование типа во множественном числе
//
Функция ОписаниеТипаПоИмени(ИмяТипаОбъектаКонфигурации) Экспорт
	
	Возврат ХэшПоиска[ВРег(ИмяТипаОбъектаКонфигурации)];
	
КонецФункции

// Выполняет приведение имени типа в различных вариациях к единому стилю
//	Возвращает вариант на английском в единственном числе
//
// Параметры:
//   ИмяТипаОбъектаКонфигурации - Строка - Имя типа объекта, например, Справочник, Document, Перечисления
//
//  Возвращаемое значение:
//   Строка - Имя типа на английском в единственном числе
//
Функция НормализоватьИмя(ИмяТипаОбъектаКонфигурации) Экспорт
	
	Описание = ХэшПоиска[ВРег(ИмяТипаОбъектаКонфигурации)];
	
	Если Описание = Неопределено Тогда		
		Возврат ИмяТипаОбъектаКонфигурации;
	Иначе
		Возврат Описание.НаименованиеEng;
	КонецЕсли;

КонецФункции

// Возвращает имя типа подсистемы
//
//  Возвращаемое значение:
//   Строка - Имя типа
//
Функция ИмяТипаПодсистемы() Экспорт
	
	Возврат НормализоватьИмя("Подсистемы");

КонецФункции

// Возвращает имя типа по-русски
//
// Параметры:
//   ИмяТипаОбъектаКонфигурации - Строка - Имя типа объекта, например, Справочник, Document, Перечисления
//
//  Возвращаемое значение:
//   Строка - Имя типа по-русски
//
Функция ПолучитьИмяТипаНаРусском(ИмяТипаОбъектаКонфигурации) Экспорт
	
	ОписаниеТипа = ОписаниеТипаПоИмени(ИмяТипаОбъектаКонфигурации);
	
	Возврат ?(ОписаниеТипа = Неопределено, ИмяТипаОбъектаКонфигурации, ОписаниеТипа.Наименование);
	
КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////

Процедура ЗагрузитьОписаниеТиповОбъектовКонфигурации()
	
	ОбъектыКонфигурации = Новый Массив();
	ХэшПоиска = Новый Соответствие();
	
	ФайлОписаний = ОбъединитьПути(ТекущийСценарий().Каталог, "..", "Макеты", "ОбъектыКонфигурации.md");
	
	Чтение = Новый ЧтениеТекста();
	Чтение.Открыть(ФайлОписаний, КодировкаТекста.UTF8);
	
	Пока Истина Цикл // Позиционируемся на нужную строку
		
		Строка = Чтение.ПрочитатьСтроку();
		
		Если Строка = Неопределено Тогда
			
			Прервать;
			
		КонецЕсли;
		
		Если Строка = "## Имена" Тогда
			
			Чтение.ПрочитатьСтроку(); // Пропускаем пустую строку
			Чтение.ПрочитатьСтроку(); // Пропускаем заголовок таблицы
			Чтение.ПрочитатьСтроку(); // Пропускаем заголовок таблицы
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Пока Истина Цикл
		
		Строка = Чтение.ПрочитатьСтроку();
		
		Если Строка = Неопределено Тогда
			
			Прервать;
			
		КонецЕсли;
		
		Если НЕ СтрНачинаетсяС(Строка, "|") Тогда
			
			Прервать;
		КонецЕсли;
		
		ЧастиСтроки = СтрРазделить(Строка, "|");
		
		Описание = Новый Структура("Наименование, НаименованиеКоллекции, НаименованиеEng, НаименованиеКоллекцииEng", 
		СокрЛП(ЧастиСтроки[1]), СокрЛП(ЧастиСтроки[2]), СокрЛП(ЧастиСтроки[3]), СокрЛП(ЧастиСтроки[4]));
		
		ОбъектыКонфигурации.Добавить(Описание);
		ХэшПоиска.Вставить(ВРег(Описание.Наименование), Описание);
		ХэшПоиска.Вставить(ВРег(Описание.НаименованиеКоллекции), Описание);
		ХэшПоиска.Вставить(ВРег(Описание.НаименованиеEng), Описание);
		ХэшПоиска.Вставить(ВРег(Описание.НаименованиеКоллекцииEng), Описание);
		
	КонецЦикла;
	
	Чтение.Закрыть();
	
КонецПроцедуры

ЗагрузитьОписаниеТиповОбъектовКонфигурации();