
Процедура ЗаписатьОписаниеКонфигурацииXML(ОписаниеКонфигурации, ИмяФайла) Экспорт
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку("UTF-8");
	ЗаписьXML.ЗаписатьОбъявлениеXML();
	ЗаписьXML.ЗаписатьНачалоЭлемента("configuration");	
	ЗаписатьЗначениеXML(ЗаписьXML, ОписаниеКонфигурации);
	ЗаписьXML.ЗаписатьКонецЭлемента();
	
	Текст = ЗаписьXML.Закрыть();
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.УстановитьТекст(Текст);
	ТекстовыйДокумент.Записать(ИмяФайла);
	
КонецПроцедуры

Процедура ЗаписатьЗначениеXML(ЗаписьXML, Знач Значение)
	
	Если ТипЗнч(Значение) = Тип("Структура") ИЛИ ТипЗнч(Значение) = Тип("Соответствие") Тогда
		
		Для Каждого Поле Из Значение Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента(Поле.Ключ);
			ЗаписатьЗначениеXML(ЗаписьXML, Поле.Значение);
			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		
	ИначеЕсли ТипЗнч(Значение) = Тип("Массив") Тогда
		
		Для Каждого Поле Из Значение Цикл
			ЗаписатьЗначениеXML(ЗаписьXML, Поле);
		КонецЦикла;

	ИначеЕсли ТипЗнч(Значение) = Тип("ТаблицаЗначений") Тогда
		
		Для Каждого Поле Из Значение Цикл
			ЗаписьXML.ЗаписатьНачалоЭлемента("row");

			Для Каждого Колонка Из Значение.Колонки Цикл
				ЗаписьXML.ЗаписатьНачалоЭлемента(Колонка.Имя);
				ЗаписатьЗначениеXML(ЗаписьXML, Поле[Колонка.Имя]);
				ЗаписьXML.ЗаписатьКонецЭлемента();
			КонецЦикла;

			ЗаписьXML.ЗаписатьКонецЭлемента();
		КонецЦикла;
		
	Иначе
		
		ЗаписьXML.ЗаписатьТекст(XMLСтрока(Значение));
		
	КонецЕсли;
	
КонецПроцедуры
