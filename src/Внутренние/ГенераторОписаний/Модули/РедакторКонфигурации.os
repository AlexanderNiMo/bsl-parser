// Добавляет объект в конфигурацию
//	И инициализирует описание объекта
//	При загрузке конфигурации, либо при последующем заполнении описания, необходимо использовать ЗарегистрироватьОбъектКонфигурации
//
// Параметры:
//   ОписаниеКонфигурации - ДанныеКонфигурации - Объект содержащий данные конфигурации (объекты, модули и т.д.)
//   ТипОбъекта - Строка - Тип объекта конфигурации, см ТипыОбъектовКонфигурации, ОбъектыКонфигурации.md
//   ОписаниеИлиИмя - Структура, Строка - Имя или данные объекта
//
//  Возвращаемое значение:
//   СтрокаТаблицыЗначений - Описание объекта конфигурации. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
Функция ДобавитьОбъект(ОписаниеКонфигурации, ТипОбъекта, ОписаниеИлиИмя) Экспорт

	Если ТипЗнч(ОписаниеИлиИмя) = Тип("Строка") Тогда
		
		ОписаниеОбъекта = СтруктурыОписаний.СоздатьОбъект(ТипОбъекта, ОписаниеИлиИмя);
		
	Иначе
		
		ОписаниеОбъекта = ОписаниеИлиИмя;

	КонецЕсли;

	Возврат ДобавитьОбъектПоОписанию(ОписаниеКонфигурации, ТипОбъекта, ОписаниеОбъекта);
	
КонецФункции

// Регистрирует объект в конфигурации
//
// Параметры:
//   ОписаниеКонфигурации - ДанныеКонфигурации - Объект содержащий данные конфигурации (объекты, модули и т.д.)
//   ТипОбъекта - Строка - Тип объекта конфигурации, см ТипыОбъектовКонфигурации, ОбъектыКонфигурации.md
//   ОписаниеИлиИмя - Структура, Строка - Имя или данные объекта
//
//  Возвращаемое значение:
//   СтрокаТаблицыЗначений - Описание объекта конфигурации. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
Функция ЗарегистрироватьОбъектКонфигурации(ОписаниеКонфигурации, ТипОбъекта, ОписаниеИлиИмя) Экспорт

	Если ТипЗнч(ОписаниеИлиИмя) = Тип("Строка") Тогда
		
		Возврат ДобавитьОбъектПоИмени(ОписаниеКонфигурации, ТипОбъекта, ОписаниеИлиИмя);
	
		
	Иначе
		
		Возврат ДобавитьОбъектПоОписанию(ОписаниеКонфигурации, ТипОбъекта, ОписаниеИлиИмя);
	
	КонецЕсли;

КонецФункции

// Перегрузка метода добавить, выполняет добавление по типу и имени
//
// Параметры:
//   ОписаниеКонфигурации - ДанныеКонфигурации - Объект содержащий данные конфигурации (объекты, модули и т.д.)
//   ТипОбъекта - Строка - Тип объекта конфигурации, см ТипыОбъектовКонфигурации, ОбъектыКонфигурации.md
//   Имя - Строка - Имя добавляемого объекта
//
//  Возвращаемое значение:
//   СтрокаТаблицыЗначений - Описание объекта конфигурации. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
Функция ДобавитьОбъектПоИмени(ОписаниеКонфигурации, ТипОбъекта, Имя) Экспорт
	
	ТипОбъекта = ТипыОбъектовКонфигурации.НормализоватьИмя(ТипОбъекта);
	
	ОбъектКонфигурации = ОписаниеКонфигурации.НайтиОбъект(Имя, ТипОбъекта);
	
	Если ОбъектКонфигурации <> Неопределено Тогда
		
		ВызватьИсключение СтрШаблон("Объект '%1.%2' уже зарегистрирован в конфигурации", ТипОбъекта, Имя);

	КонецЕсли;
	
	Конфигурация = ОписаниеКонфигурации.Конфигурация;

	ОбъектКонфигурации = ОписаниеКонфигурации.ОбъектыКонфигурации.Добавить();
	ОбъектКонфигурации.Наименование = Имя;
	ОбъектКонфигурации.Конфигурация = Конфигурация;
	ОбъектКонфигурации.Тип = ТипОбъекта;
	ОбъектКонфигурации.ПутьКФайлу = Конфигурация.СтруктураКаталогов().ИмяФайлаОписанияОбъекта(Имя, ТипОбъекта);
	ОбъектКонфигурации.ПутьККаталогу = Конфигурация.СтруктураКаталогов().КаталогФайловОбъекта(Имя, ТипОбъекта);
	
	ОбъектКонфигурации.ПолноеНаименование = ТипОбъекта + "." + ОбъектКонфигурации.Наименование;

	Возврат ОбъектКонфигурации;

КонецФункции

// Перегрузка метода добавить, выполняет добавление по типу и данным объекта
//
// Параметры:
//   ОписаниеКонфигурации - ДанныеКонфигурации - Объект содержащий данные конфигурации (объекты, модули и т.д.)
//   ТипОбъекта - Строка - Тип объекта конфигурации, см ТипыОбъектовКонфигурации, ОбъектыКонфигурации.md
//   ОписаниеОбъекта - Структура - Данные описания объекта
//
//  Возвращаемое значение:
//   СтрокаТаблицыЗначений - Описание объекта конфигурации. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
Функция ДобавитьОбъектПоОписанию(ОписаниеКонфигурации, ТипОбъекта, ОписаниеОбъекта) Экспорт
	
	ОбъектКонфигурации = ДобавитьОбъектПоИмени(ОписаниеКонфигурации, ТипОбъекта, ОписаниеОбъекта.Наименование);
	ОбъектКонфигурации.Описание = ОписаниеОбъекта;

	Возврат ОбъектКонфигурации;

КонецФункции

// Выполняет настройку объекта для создания новой конфигурации
//
// Параметры:
//   Конфигурация - Конфигурация - Описание конфигурации
//
Процедура ИнициализироватьСозданиеНового(Конфигурация) Экспорт

	СвойстваКонфигурации = СтруктурыОписаний.СоздатьОбъект(ТипыОбъектовКонфигурации.ИмяТипаКонфигурации(), "Конфигурация");
	Конфигурация.ОписаниеКонфигурации().СвойстваКонфигурации = СвойстваКонфигурации;

КонецПроцедуры