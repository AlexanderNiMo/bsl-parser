///////////////////////////////////////////////////////////////////////////////
//
// Реализует специфику редактирования расширения
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Добавляет объект конфигурации в расширение, для переопределения, либо использования
//
// Параметры:
//   ОписаниеКонфигурации - ДанныеКонфигурации - Объект содержащий данные конфигурации (объекты, модули и т.д.)
//   ОбъектРодительскойКонфигурации - СтрокаТаблицыЗначений - Описание объекта родительской конфигурации. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
//  Возвращаемое значение:
//   СтрокаТаблицыЗначений - Описание объекта расширения. См. СтруктурыОписаний.ТаблицаОписанияОбъектовКонфигурации
//
Функция ДобавитьОбъектВРасширение(ОписаниеКонфигурации, ОбъектРодительскойКонфигурации) Экспорт
	
	ТипОбъекта = ОбъектРодительскойКонфигурации.Тип;
	Имя = ОбъектРодительскойКонфигурации.Наименование;
	
	Если ОписаниеКонфигурации.НайтиОбъект(Имя, ТипОбъекта) <> Неопределено Тогда
		
		ВызватьИсключение СтрШаблон("Объект '%1.%2' уже зарегистрирован в конфигурации", ТипОбъекта, Имя);

	КонецЕсли;
	
	ДанныеОбъекта = СтруктурыОписаний.СоздатьОбъектДляВключенияВРасширение(ТипОбъекта, Имя);
	
	Если ОбъектРодительскойКонфигурации.Описание <> Неопределено Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ОбъектРодительскойКонфигурации.Описание, , "Подчиненные");

	КонецЕсли;

	ДанныеОбъекта.Принадлежность = "Adopted";
	
	ОбъектРасширения = РедакторКонфигурации.ДобавитьОбъект(ОписаниеКонфигурации, ТипОбъекта, ДанныеОбъекта);

	Возврат ОбъектРасширения;

КонецФункции

// Выполняет настройку объекта для создания нового расширения
//
// Параметры:
//   Расширение - Расширение - Описание расширения
//
Процедура ИнициализироватьСозданиеНового(Расширение) Экспорт

	СвойстваРасширения = СтруктурыОписаний.СоздатьОбъект(ТипыОбъектовКонфигурации.ИмяТипаРасширения(), "Расширение");
	СвойстваРасширения.Принадлежность = "Adopted";
	СвойстваРасширения.Назначение = "Customization";

	Расширение.ОписаниеКонфигурации().СвойстваКонфигурации = СвойстваРасширения;

КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////
