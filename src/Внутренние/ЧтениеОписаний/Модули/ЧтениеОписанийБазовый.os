///////////////////////////////////////////////////////////////////////////////
//
// Общие методы чтения файлов описаний
//
///////////////////////////////////////////////////////////////////////////////

Перем Рефлектор;

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Возвращает полное наименование объекта конфигурации или модуля
//
// Параметры:
//   СтрокаОбъект - СтрокаТаблицыЗначений - Описание объекта или модуля конфигурации
//   ДобавлятьПрефиксДляОбщихМодулей - Булево - Признак, добавлять ли тип объекта для общих модулей
//
//  Возвращаемое значение:
//   Строка - Полное имя
//
Функция ПолноеИмяОбъекта(СтрокаМодуль, ДобавлятьПрефиксДляОбщихМодулей = Истина, Разделитель = ".") Экспорт
	
	Если Утилиты.ПеременнаяСодержитСвойство(СтрокаМодуль, "ТипМодуля") Тогда // Передано описание модуля
		
		Если СтрокаМодуль.ТипМодуля = ТипыМодуля.ОбщийМодуль И НЕ ДобавлятьПрефиксДляОбщихМодулей Тогда

			Возврат СтрокаМодуль.Родитель.Наименование;
			
		КонецЕсли;
		
		ПолноеИмя = ТипыОбъектовКонфигурации.ПолучитьИмяТипаНаРусском(СтрокаМодуль.Родитель.Тип) + Разделитель + СтрокаМодуль.Родитель.Наименование;

		Если СтрокаМодуль.РодительФорма <> Неопределено Тогда
		
			ПолноеИмя = ПолноеИмя + Разделитель + СтрокаМодуль.РодительФорма.Наименование;
			
		ИначеЕсли СтрокаМодуль.РодительКоманда <> Неопределено Тогда
		
			ПолноеИмя = ПолноеИмя + Разделитель + СтрокаМодуль.РодительКоманда.Наименование;
			
		КонецЕсли;

		Возврат ПолноеИмя;
		
	Иначе
		
		Возврат ТипыОбъектовКонфигурации.ПолучитьИмяТипаНаРусском(СтрокаМодуль.Тип) + "." + СтрокаМодуль.Наименование;
		
	КонецЕсли;

КонецФункции

// Возвращает объект на основании считанных из описания данных
//
// Параметры:
//   СырыеДанные - ТаблицаЗначений - Считанные из файла описаний данные
//   ПараметрыЧтения - Структура - Описание трансформации данных описания в объект. См. ПараметрыСериализации.ПараметрыСериализации
//
//  Возвращаемое значение:
//   Структура - Данные объекта
//
Функция ОбработатьСырыеДанные(СырыеДанные, ПараметрыЧтения) Экспорт
	
	ДанныеОбъекта = СтруктурыОписаний.СоздатьОбъект(ПараметрыЧтения.Тип, "<Не инициализированный>");

	Для Каждого Запись Из СырыеДанные Цикл
		
		Параметр = Неопределено;
		
		Если НЕ ПараметрыЧтения.Свойства.Свойство(Запись.Ключ, Параметр) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Параметр.ЭтоКоллекция И ТипЗнч(Запись.Значение) = Тип("Массив") Тогда

			Для каждого Значение Из Запись.Значение Цикл
				
				ДанныеОбъекта[Параметр.Наименование].Добавить(Значение);
				
			КонецЦикла;
			
		ИначеЕсли Параметр.ЭтоКоллекция Тогда

			ДанныеОбъекта[Параметр.Наименование].Добавить(Запись.Значение);
			
		Иначе
		
			ДанныеОбъекта[Параметр.Наименование] = Запись.Значение;

		КонецЕсли;
				
	КонецЦикла;

	Возврат ДанныеОбъекта;
	
КонецФункции

// Выполняет чтение данных описания
//
// Параметры:
//   ЧтениеXML - ЧтениеXML - Читатель данных
//   ПараметрыЧтения - Структура - Описание трансформации данных описания в объект. См. ПараметрыСериализации.ПараметрыСериализации
//   Читатель - Модуль - Модуль реализующий интерфейс преобразования данных
//
//  Возвращаемое значение:
//   ТаблицаЗначений - Значения свойств описания объекта
//
Функция ПрочитатьСвойстваXML(ЧтениеXML, ПараметрыЧтения, Читатель) Экспорт
	
	ПараметрыПреобразования = ПараметрыПреобразованияПриЧтении(ПараметрыЧтения, Читатель);
	
	УровеньКорня = ЧтениеXML.КонтекстПространствИмен.Глубина - 1; // Окончание элемента, которое нам нужно поймать имеет уровень -1 от текущего
	ИмяКорня = ЧтениеXML.ЛокальноеИмя;
	
	Значения = Новый ТаблицаЗначений();
	Значения.Колонки.Добавить("Ключ");
	Значения.Колонки.Добавить("Значение");

	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ИмяКорня = ЧтениеXML.ЛокальноеИмя И УровеньКорня = ЧтениеXML.КонтекстПространствИмен.Глубина Тогда
			Прервать;
		КонецЕсли;
		
		Если УровеньКорня >= ЧтениеXML.КонтекстПространствИмен.Глубина Тогда
			ВызватьИсключение "Ошибка чтения свойств. Ошибка алгоритма преобразования";
		КонецЕсли;
		
		Если ЧтениеXML.ТипУзла <> ТипУзлаXML.НачалоЭлемента Тогда
			Продолжить;
		КонецЕсли;

		КлючСвойства = ЧтениеXML.ЛокальноеИмя;
		
		Если ПараметрыПреобразования.Свойство(КлючСвойства) Тогда
			
			Значение = ПреобразоватьЗначение(ЧтениеXML, ПараметрыПреобразования[КлючСвойства], Читатель);
			
		Иначе

			ЧтениеXML.Прочитать();
			
			Если ЧтениеXML.ИмеетЗначение Тогда

				Значение = ЧтениеXML.Значение;
				ЧтениеXML.Прочитать();
				
			Иначе
				
				Пока УровеньКорня + 1 <> ЧтениеXML.КонтекстПространствИмен.Глубина Цикл
					ЧтениеXML.Пропустить();
				КонецЦикла;

				Продолжить;

			КонецЕсли;

		КонецЕсли;
		
		ЗаписьЗначения = Значения.Добавить();
		ЗаписьЗначения.Ключ = КлючСвойства;
		ЗаписьЗначения.Значение = Значение;
		
	КонецЦикла;

	Возврат Значения;
	
КонецФункции

// Читает значение вложенного элемента
//
// Параметры:
//   ЧтениеXML - ЧтениеXML - Читатель данных
//
//  Возвращаемое значение:
//   Произвольный - Данные вложенного элемента
//
Функция ЗначениеВложенногоТэга(Знач ЧтениеXML, ИмяТега) Экспорт
	
	Имя = ЧтениеXML.Имя;

	Пока ЧтениеXML.Имя <> ИмяТега Цикл
		
		Если ЧтениеXML.Имя = Имя И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Возврат "";
			
		КонецЕсли;

		ЧтениеXML.Прочитать();
		
	КонецЦикла;
	
	ЧтениеXML.Прочитать();
	Значение = ЧтениеXML.Значение;
	
	ЧтениеXML.Прочитать();

	Возврат Значение;

КонецФункции

// Формирует привычное имя типа из XML описания
//
// Параметры:
//   ТипИзXML - Строка- Имя типа XML
//
//  Возвращаемое значение:
//   Строка - Дружелюбное имя
//
Функция ПреобразоватьТип(ТипИзXML, ФорматEDT = Ложь) Экспорт

	// TODO: Проверить для EDT
	ПримитивныеТипы = Новый Соответствие;

	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "boolean", "Булево");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "decimal", "Число");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "number", "Число");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "string", "Строка");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "datetime", "Дата");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "xs:", "") + "date", "Дата");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "v8:", "") + "valuestorage", "Хранилище Значений");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "v8:", "") + "uuid", "UUID");
	ПримитивныеТипы.Вставить(?(НЕ ФорматEDT, "v8:", "") + "null", "Null");

	ПреобразованныйТип = ПримитивныеТипы[НРег(ТипИзXML)];
	
	Если ПреобразованныйТип = Неопределено И ФорматEDT И СтрНайти(ТипИзXML, ".") Тогда

		ЧастиТипа = СтрРазделить(ТипИзXML, ".");
		ПреобразованныйТип = ТипыОбъектовКонфигурации.ПолучитьИмяТипаНаРусском(СтрЗаменить(ЧастиТипа[0], "Ref", "")) + "." + ЧастиТипа[1];

	ИначеЕсли НЕ ФорматEDT И СтрНачинаетсяС(ТипИзXML, "cfg:") Тогда

		ТипИзXML = СтрЗаменить(ТипИзXML, "cfg:", "");
		ЧастиТипа = СтрРазделить(ТипИзXML, ".");

		ПреобразованныйТип = ТипыОбъектовКонфигурации.ПолучитьИмяТипаНаРусском(СтрЗаменить(ЧастиТипа[0], "Ref", "")) + "." + ЧастиТипа[1];
		
	КонецЕсли;
	
	Если ПреобразованныйТип = Неопределено Тогда

		ПреобразованныйТип = ТипИзXML;

	КонецЕсли;

	Возврат ПреобразованныйТип;

КонецФункции

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////

// Формирует параметры для кастомного преобразования данных.
//
// Параметры:
//   ПараметрыЧтения - Структура - Описание трансформации данных описания в объект. См. ПараметрыСериализации.ПараметрыСериализации
//   Читатель - Модуль - Модуль реализующий интерфейс преобразования данных
//
//  Возвращаемое значение:
//   Структура - Параметры преобразования
//
Функция ПараметрыПреобразованияПриЧтении(ПараметрыЧтения, Читатель)
	
	ОбработчикиПолей = Новый Структура();
	
	Для Каждого Элемент Из ПараметрыЧтения.Свойства Цикл
		
		Параметр = Элемент.Значение;

		Если НЕ ПустаяСтрока(Параметр.МетодПреобразования) И Рефлектор.МетодСуществует(Читатель, Параметр.МетодПреобразования) Тогда
			
			ОбработчикиПолей.Вставить(ВРег(Параметр.Поле), Параметр.МетодПреобразования);
			
		КонецЕсли;
		
	КонецЦикла;

	Возврат ОбработчикиПолей;
	
КонецФункции // ОбработчикиПреобразований

Функция ПреобразоватьЗначение(ЧтениеXML, МетодПреобразования, Читатель)
	
	ЗначениеВМассиве = Новый Массив(1);
	ЗначениеВМассиве[0] = ЧтениеXML;

	Значение = Рефлектор.ВызватьМетод(Читатель, МетодПреобразования, ЗначениеВМассиве);
	
	Возврат Значение;
	
КонецФункции

Рефлектор = Новый Рефлектор();
