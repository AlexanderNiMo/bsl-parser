///////////////////////////////////////////////////////////////////////////////
//
// Модуль содержит полезные методы для работы с XML
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

Функция ПерейтиКСледующемуЭлементу(Чтение, ИмяЭлемента) Экспорт
	
	Уровень = 0;

	Пока Чтение.Прочитать() Цикл

		Если Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			Уровень = Уровень + 1;
			
			Если Уровень = 1 И Чтение.Имя = ИмяЭлемента Тогда
			
				Возврат Истина;
				
			КонецЕсли
			
		ИначеЕсли Чтение.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Уровень = Уровень - 1;
			
			Если Уровень = -1 Тогда
				
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;

КонецФункции

Процедура СкопироватьДанныеXML(Чтение, Запись, КопироватьВсеЭлементыУровня = Ложь) Экспорт
	
	Уровень = 0;
	УровеньВыхода = ?(КопироватьВсеЭлементыУровня, -1, 0);

	Пока Истина Цикл
		
		Если Чтение.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			Уровень = Уровень + 1;
			
			Запись.ЗаписатьНачалоЭлемента(Чтение.Имя);
			
			Пока Чтение.ПрочитатьАтрибут() Цикл
				
				Запись.ЗаписатьАтрибут(Чтение.Имя, Чтение.Значение);

			КонецЦикла;
			
		ИначеЕсли Чтение.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Уровень = Уровень - 1;
			
			Если Уровень = УровеньВыхода Тогда
				
				Прервать;
				
			КонецЕсли;
			
			Запись.ЗаписатьКонецЭлемента();

		ИначеЕсли Чтение.ТипУзла = ТипУзлаXML.Текст Тогда
			
			Запись.ЗаписатьТекст(Чтение.Значение);
			
		КонецЕсли;

		Чтение.Прочитать();

	КонецЦикла;
	
КонецПроцедуры

// Записывает произвольное значение в XML поток
//
// Параметры:
//   Запись - ЗаписьXML - Поток записи XML
//   Имя - Строка - Имя реквизита, тэг
//   Значение - Произвольный - Записываемое значение
//
Процедура ЗаписатьЗначениеXML(Запись, Имя, Значение) Экспорт
	
	Запись.ЗаписатьНачалоЭлемента(Имя);
	Запись.ЗаписатьТекст(XMLСтрока(Значение));
	Запись.ЗаписатьКонецЭлемента();
	
КонецПроцедуры

// Закрывает запись описания объекта и всех открытых тэгов
//
// Параметры:
//   Запись - ЗаписьXML - запись описания объекта
//
//  Возвращаемое значение:
//   Строка - XML описание объекта
//
Функция ЗакрытьЗапись(Запись) Экспорт

    Для Инд = 1 По Запись.КонтекстПространствИмен.Глубина Цикл

        Запись.ЗаписатьКонецЭлемента();

    КонецЦикла;

    Возврат Запись.Закрыть();

КонецФункции




///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////
