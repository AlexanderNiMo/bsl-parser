///////////////////////////////////////////////////////////////////////////////
//
// Модуль содержащий методы определения параметров чтения/записи объектов
//
///////////////////////////////////////////////////////////////////////////////

///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС
///////////////////////////////////////////////////////////////////////////////

// Формирует параметры для чтения описания объекта конкретного типа
//
// Параметры:
//   ТипОбъекта - Строка - Тип объекта
//   Формат - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//
//  Возвращаемое значение:
//   Структура - Параметры для чтения описания объекта
//		* Тип - Строка - Имя типа
//		* ЕстьПодчиненные - Булево - Тип содержит подчиненные элементы
//		* ЕстьЗначенияПоУмолчанию - Булево - У свойств есть значения по-умолчанию
//		* Свойства - Массив - Коллекция свойств объекта. Структура свойства
//			* Наименование - Имя свойства
//			* МетодПреобразования - Имя метода чтения
//			* Поле - Имя тэга файла описаний
//			* ЭтоКоллекция - Содержит несколько значений
//
Функция ПараметрыЧтения(ТипОбъекта, Формат) Экспорт
	
	Возврат ПараметрыСериализации(ТипОбъекта, Формат, Истина);
	
КонецФункции // ПараметрыЧтения

// Формирует параметры для записи описания объекта конкретного типа
//
// Параметры:
//   ТипОбъекта - Строка - Тип объекта
//   Формат - Строка - Формат выгрузки. Значение перечисления ФорматыВыгрузки
//
//  Возвращаемое значение:
//   Структура - Параметры для записи описания объекта
//		* Тип - Строка - Имя типа
//		* ЕстьПодчиненные - Булево - Тип содержит подчиненные элементы
//		* ЕстьЗначенияПоУмолчанию - Булево - У свойств есть значения по-умолчанию
//		* Свойства - Массив - Коллекция свойств объекта. Структура свойства
//			* Наименование - Имя свойства
//			* МетодПреобразования - Имя метода записи
//			* Поле - Имя тэга файла описаний
//			* ЭтоКоллекция - Содержит несколько значений
//
Функция ПараметрыЗаписи(ТипОбъекта, Формат) Экспорт
	
	Возврат ПараметрыСериализации(ТипОбъекта, Формат, Ложь);
	
КонецФункции // ПараметрыЗаписи

///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
///////////////////////////////////////////////////////////////////////////////

Функция ПараметрыСериализации(ТипОбъекта, Формат, ДляЧтения)
	
	ДанныеОписания = Новый Структура("Тип, Свойства, ЕстьПодчиненные, ЕстьЗначенияПоУмолчанию");
	
	ОписаниеОбъекта = СтруктурыОписаний.ОписаниеСвойствОбъекта(ТипОбъекта);
	
	ЗаполнитьЗначенияСвойств(ДанныеОписания, ОписаниеОбъекта, , "Свойства");
	
	РеквизитыСвойства = "Наименование, МетодПреобразования, Поле, ЭтоКоллекция";
	
	РеквизитИмениПоля = ?(Формат = ФорматыВыгрузки.EDT, "ЭлементEDT", "ЭлементDesigner");
	
	Если ОписаниеОбъекта.ЕстьЗначенияПоУмолчанию Тогда
		РеквизитыСвойства = РеквизитыСвойства + ", ЗначенияПоУмолчанию";
	КонецЕсли;
	
	ДанныеОписания.Свойства = Новый Структура();

	Для Каждого Свойство Из ОписаниеОбъекта.Свойства Цикл
		
		ОписаниеСвойства = Новый Структура(РеквизитыСвойства);
		ЗаполнитьЗначенияСвойств(ОписаниеСвойства, Свойство);
		
		ОписаниеСвойства.Поле = Свойство[РеквизитИмениПоля];
		
		Если ДляЧтения Тогда
			
			ДанныеОписания.Свойства.Вставить(ОписаниеСвойства.Поле, ОписаниеСвойства);

		Иначе
			
			ДанныеОписания.Свойства.Вставить(ОписаниеСвойства.Наименование, ОписаниеСвойства);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ДанныеОписания;
	
КонецФункции

